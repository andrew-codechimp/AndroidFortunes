apply plugin: 'com.android.application'

project.archivesBaseName = "AndroidFortunes";

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        applicationId "org.codechimp.androidfortunes"
        minSdkVersion 18
        targetSdkVersion 20
        versionName project.VERSION_NAME
        versionCode Integer.parseInt(project.VERSION_CODE)
    }

    signingConfigs { release }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    wearApp project(':wear')
    compile "com.android.support:support-v4:22.1.1"
    compile 'com.google.android.gms:play-services-wearable:7.0.0'
    compile 'com.squareup.retrofit:retrofit:1.5.1@jar'
    compile 'com.google.code.gson:gson:2.3'
    compile 'com.squareup.okhttp:okhttp:1.3.0'
    compile 'com.jakewharton:butterknife:5.1.0'
}


def Properties props = new Properties()
props.load(new FileInputStream(file('signing.properties')))

if (props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
        props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {
    android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
    android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
    android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
    android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
} else {
    android.buildTypes.release.signingConfig = null
}
